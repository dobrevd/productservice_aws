plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.0'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.dobrev'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	lombokVersion = '1.18.30'
	mapstructVersion = '1.5.5.Final'
	awsSdkBomVersion = '2.29.37'
}

dependencies {
	// Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'

	// AWS SDK
	implementation platform("software.amazon.awssdk:bom:${awsSdkBomVersion}")
	implementation 'software.amazon.awssdk:dynamodb'
	implementation 'software.amazon.awssdk:dynamodb-enhanced'
	implementation 'software.amazon.awssdk:sns'

	// AWS X-Ray
	implementation 'com.amazonaws:aws-xray-recorder-sdk-spring:2.18.2'
	implementation 'com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2:2.18.2'

	// Logging
	implementation 'org.slf4j:slf4j-api:2.0.5'

	// Annotation Processors
	implementation "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.mockito:mockito-inline:5.2.0'
	testImplementation 'org.mockito:mockito-core:5.14.2'
}

tasks.named('test') {
	useJUnitPlatform()
}

configurations.configureEach {
	exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
	exclude group: 'commons-logging', module: 'commons-logging'
}

tasks.register("unpack", Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/libs")
}